config {
    type: "incremental",
    uniqueKey: ['dt', 'event_id'],
    schema: constants.WORK_SCHEMA,
    protected: true,
    bigquery: {
        partitionBy: "dt",
    }
}

SELECT
  FARM_FINGERPRINT(CONCAT(event_timestamp, event_name, user_pseudo_id)) AS event_id,
  TIMESTAMP_MICROS(event_timestamp) AS ts,
  DATE(TIMESTAMP_MICROS(event_timestamp)) AS dt,
  CONCAT((
    SELECT
      ep.value.int_value AS ga_session_id
    FROM
      UNNEST(event_params) ep
    WHERE
      ep.key = 'ga_session_id'), user_pseudo_id) AS session_id,
  device.category AS device_category,
  ${helpers.getEventParam('page_location')},
  ${helpers.getEventParam('page_referrer')},
  ${helpers.getEventParam('page_title')},
FROM
  ${when(incremental(),  `${ref(constants.GA4_TABLE)}`)} 
  ${when(!incremental(), `${ref('events_*')}`)}
WHERE
  event_name = 'page_view' ${when(!incremental(), "and contains_substr(_TABLE_SUFFIX, 'intraday') is not true")}
